apply plugin: "java"
apply plugin: "signing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "DiscordMusic"
version = "2.0.5"

configurations {
    provided {
        compile.extendsFrom(provided)
    }
}

repositories {
    jcenter()
}

dependencies {
    compile("com.google.code.gson:gson:2.8.5")
    compile("com.sedmelluq:lavaplayer:1.2.63")
    compile("net.dv8tion:JDA:3.6.0_367")
    compile("org.apache.commons:commons-lang3:3.7")
    annotationProcessor("org.apache.logging.log4j:log4j-core:2.11.0")
    compile("org.apache.logging.log4j:log4j-core:2.11.0")
    compile("org.apache.logging.log4j:log4j-slf4j-impl:2.11.0")
}

jar {
    manifest {
        attributes("Main-Class": "io.github.lxgaming.discordmusic.Main")
    }

    from {
        (configurations.compile - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    exclude("natives/darwin/**")
    exclude("natives/linux-arm/**")
    exclude("natives/linux-x86/**")
    //exclude("natives/linux-x86-64/**")
    exclude("natives/win-x86/**")
    exclude("natives/win-x86-64/**")
    exclude("natives/win32-x86/**")
    exclude("natives/win32-x86-64/**")
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-" + archivesBaseName)
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        jar.outputs.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}